plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

// Project version is set via the VERSION environment
// variable in the Github Actions workflow and should not be changed here.
def projectVersion = System.getenv("VERSION") ?: '1.0.0'
def runeLiteVersion = '1.11.24.1' // latest.release
def shortestPathPluginVersion = '1.0.3'
def packetUtilsVersion = '1.0.8'

version = projectVersion
project.version = projectVersion


repositories {
    mavenCentral()
    maven {
        url = 'https://repo.runelite.net'
    }
    maven { url 'https://jitpack.io' }
    mavenLocal()
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.18.1'
    implementation group: 'com.google.inject', name: 'guice', version: '4.2.2'
    implementation("com.google.code.gson:gson:2.10.1")
    implementation("org.benf:cfr:0.152")
    implementation("com.google.guava:guava:31.1-jre")
    implementation("org.slf4j:slf4j-api:1.7.25")
    implementation("org.slf4j:slf4j-simple:1.7.25")
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'


    implementation group: 'com.github.cbartram', name:'shortest-path', version: shortestPathPluginVersion
    implementation group: 'com.github.cbartram', name:'packet-utils', version: packetUtilsVersion
    implementation group: 'net.runelite', name:'client', version: runeLiteVersion
    testImplementation 'junit:junit:4.12'
    testImplementation group: 'net.runelite', name:'client', version: runeLiteVersion
    testImplementation group: 'net.runelite', name:'jshell', version: runeLiteVersion
}

testing {
    suites {
        test {
            useJUnit('4.13.2')
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

jar {
    archiveBaseName = 'kraken-api'
    archiveVersion = projectVersion
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = 'com.github.cbartram'
            artifactId = 'kraken-api'
            version = project.version

            pom {
                name = 'Kraken API'
                description = 'Kraken API Library'
                url = 'https://github.com/cbartram/kraken-api'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'cbartram'
                        name = 'Rune Wraith'
                        email = 'runewraith.business@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/cbartram/kraken-api.git'
                    developerConnection = 'scm:git:ssh://github.com/cbartram/kraken-api.git'
                    url = 'https://github.com/cbartram/kraken-api'
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cbartram/kraken-api")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
